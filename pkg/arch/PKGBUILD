# Maintainer: Pierre Lalet <pierre.lalet@cea.fr>
#
# Arch Linux package file

# This file is part of IVRE.
# Copyright 2011 - 2018 Pierre LALET <pierre.lalet@cea.fr>
#
# IVRE is free software: you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# IVRE is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
# or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License
# along with IVRE. If not, see <http://www.gnu.org/licenses/>.

pkgname='ivre'
pkgver=0.9.12
pkgrel=1
pkgdesc='Network recon framework'
arch=('any')
url='https://ivre.rocks/'
license=('GPL3')
depends=('python' 'python-pymongo' 'python-future' 'python-bottle')
optdepends=('python-py2neo: experimental flow analysis (Neo4j backend)'
            'python-sqlalchemy: experimental PostgreSQL & SQLite backends'
            'python-psycopg2: experimental PostgreSQL backend'
            'python-pillow: trim screenshots on insertion'
            'tesseract: extract words from screenshots on insertion'
            'python-crypto: extract data from public keys ("ivre getmoduli")'
            'python-scapy: parse PCAP files for ARP inspection (flow analysis)'
            'python-matplotlib: create graphs from command line tools'
            'python-dbus: 3D traceroute graphs'
            'mongodb: database server'
            'postgresql: database server (experimental backend)'
            'neo4j-community: database server (experimental flow analysis)'
            'apache: Web server'
            'mod_wsgi: Web server'
            'dokuwiki: Web server (notebook)'
            'nmap: Network scan'
            'masscan: Network scan'
            'zmap: Network scan'
            'bro: Network traffic analysis'
            'argus: Network traffic analysis'
            'nfdump: Netflow analysis'
            'imagemagick: Screenshots via Nmap scripts'
            'phantomjs: HTTP screenshots via Nmap script'
            'ffmpeg: RTSP Screenshots via Nmap script')
makedepends=('git' 'python-setuptools')
backup=('etc/httpd/conf/extra/ivre.conf')
branch="`git branch | awk '/^*/ {print $2}'`"
source=("git+file://`readlink -f ../../`#branch=$branch")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  python setup.py --version | sed "s/\.dev/.dev_${branch//-/_}_/"
}

build() {
  cd "$srcdir/$pkgname"
  python setup.py build
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" doc/LICENSE*
  install -Dm0644 "pkg/apache/ivre.conf" "$pkgdir/etc/httpd/conf/extra/ivre.conf"
  python setup.py install --root="$pkgdir" --optimize=1
  sed -i "s/\.dev/.dev_${branch//-/_}_/" \
      "${pkgdir}"/usr/lib/python*/site-packages/ivre/{VERSION,__init__.py}
}
